buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "https://maven.minecraftforge.net/"
        }
    }
    dependencies {
        classpath ('com.anatawa12.forge:ForgeGradle:1.2-1.1.+') {
            changing = true
        }
    }
}

plugins {
    id 'java-library'
    id 'maven-publish'
}

apply plugin: 'forge'

repositories {
    maven {
        url 'https://jitpack.io'
    }
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

version = project.version
group = project.group

sourceSets {
    main {
        java {
            srcDirs += 'src/main/java'
            srcDirs += new File(file('..'), "deps/json/src")
            srcDirs += new File(file('..'), "/common/src")
        }
        resources {
            srcDirs += 'src/main/resources'
            srcDirs += new File(file('..'), "/common/res")
        }
    }
}

minecraft {
    version = project.minecraft_version + "-" + project.forge_version
    runDir = "run"
}

dependencies {
    implementation("com.github.LegacyModdingMC.UniMixins:unimixins-all-1.7.10:$project.unimixinsVersion:dev")
}

runClient {
    args("--tweakClass", "org.spongepowered.asm.launch.MixinTweaker")
}

runServer {
    args("--tweakClass", "org.spongepowered.asm.launch.MixinTweaker")
}

def outSrgFile = "${tasks.compileJava.temporaryDir}/outSrg.srg"
def outRefMapFile = "${tasks.compileJava.temporaryDir}/mixins.${project.modid}.refmap.json"

jar {
    manifest {
        attributes (
                'TweakClass': 'org.spongepowered.asm.launch.MixinTweaker',
                'MixinConfigs': "${project.modid}.mixins.json",
        )
    }
    from outRefMapFile;
}

tasks.compileJava.options.compilerArgs += ["-AreobfSrgFile=${tasks.reobf.srg}", "-AoutSrgFile=${outSrgFile}", "-AoutRefMapFile=${outRefMapFile}"];

reobf {
    addExtraSrgFile outSrgFile
}

processResources {
    // This will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // Replace values in only mcmod.info.
    filesMatching('mcmod.info') {
        // Replace version and mcversion.
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
}

// Ensures that the encoding of source files is set to UTF-8, see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

// This block configures any maven publications you want to make.
publishing {
    publications {
        mavenJava(MavenPublication) {
        // Add any other artifacts here that you would like to publish!
            artifact(jar) {
                builtBy build
            }
        }
    }

    // This block selects the repositories you want to publish to.
    repositories {
        // Add the repositories you want to publish to here.
    }
}
